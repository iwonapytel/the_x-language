-- JPP 2018 - Interpreter, language grammar
-- Iwona Pytel, ip360730

-- Sources: https://github.com/BNFC/bnfc/blob/master/examples/C/C.cf
--          https://www.mimuw.edu.pl/~ben/Zajecia/Mrj2017/Latte/Latte.cf

-- Main program semantics
entrypoints Program, Stm, Exp;

Progr.  Program ::= [Instruction] ;

(:[]).    [Instruction] ::= Instruction;
(:).      [Instruction] ::= Instruction [Instruction];

StmInstruction.      Instruction ::= Stm ;
ExpInstruction.      Instruction ::= Exp ";" ;
DecInstruction.      Instruction ::= Dec ;
BlockInstruction.    Instruction ::= Block ;

BBlock. Block ::= "{" [Instruction] "}" ;
EmptyBlock. Block ::= "{" "}" ;


-- Types
TInt.    TypeName ::= "int";
TBool.   TypeName ::= "bool";
TString. TypeName ::= "string";
TAuto.   TypeName ::= "auto" ;
TTuple.  TypeName ::= "(" [TypeName] ")" ;
separator nonempty TypeName ",";


-- Declarations
VarDec. Dec ::= TypeName Item ";" ;
FuncDec. Dec ::= TypeName Func ;
-- StructDec. Dec ::= Struct ";"

UninitedVar.  Item ::= Ident ;
UninitedArr.  Item ::= Ident ArrSize ;
InitedVar.  Item ::= Ident "=" Exp ;
InitedArr.  Item ::= Ident ArrSize "=" Exp ;
separator nonempty Item "," ;

SimpleArg. Arg ::= TypeName Ident ;
ArrArg.    Arg ::= TypeName Ident "[]" ;
separator Arg "," ;

ValArrSize.      ArrSize ::= "[" Exp "]" ;
EmptyArrSize.    ArrSize ::= "[""]" ;

separator nonempty ArrSize "" ;

FunctionNoParams. Func ::=  Ident "(" ")" Block;
FunctionParams. Func ::=  Ident "(" [Arg] ")" Block;

-- Struct.   Struct ::= "struct" Ident "{" [Arg] "}" ;

InitExpr.    Initializer ::= Exp2 ;
InitListOne. Initializer ::= "{" Initializers "}" ;
InitListTwo. Initializer ::= "{" Initializers "," "}" ;

AnInit.   Initializers ::= Initializer ;
MoreInit. Initializers ::= Initializers "," Initializer ;

separator Dec "" ;

-- Statements
EmptyStm.  Stm ::= ";" ;
-- DecStm. Stm ::= Dec ;
SelectStm. Stm ::= SelectionStm ;
ItStm.     Stm ::= IterStm ;
RStm.      Stm ::= ReturnStm ;
PrintStm.  Stm ::= "print" "(" Exp ")" ";" ;

IfStm.        SelectionStm ::= "if" "(" Exp ")" Instruction ;
IfElseStm.    SelectionStm ::= "if" "(" Exp ")" Instruction "else" Instruction ;

WhileStm.     IterStm ::= "while" "(" Exp ")" Instruction;
ForStm.       IterStm ::= "for" "(" Exp ";" Exp ")" Instruction;
ForStmPrecond. IterStm ::= "for" "(" Exp ";" Exp ";" Exp ")" Instruction;

ContStm.   ReturnStm ::= "continue" ";" ;
BreakStm.  ReturnStm ::= "break" ";" ;
RetStm.    ReturnStm ::= "return" ";" ;
RetExpStm. ReturnStm ::= "return" Exp ";" ;

separator Stm "" ;

-- Expressions
Easgn.       Exp   ::= Exp19 AsgnOp Exp;
Elor.        Exp2  ::= Exp2 "||" Exp5;
Eland.       Exp5  ::= Exp5 "&&" Exp6;
Eeq.         Exp9  ::= Exp9 "==" Exp10;
Eneq.        Exp9  ::= Exp9 "!=" Exp10;
Elthen.      Exp10 ::= Exp10 "<" Exp11;
Egrthen.     Exp10 ::= Exp10 ">" Exp11;
Ele.         Exp10 ::= Exp10 "<=" Exp11;
Ege.         Exp10 ::= Exp10 ">=" Exp11;
Eplus.       Exp12 ::= Exp12 "+" Exp13;
Eminus.      Exp12 ::= Exp12 "-" Exp13;
Etimes.      Exp13 ::= Exp13 "*" Exp14;
Ediv.        Exp13 ::= Exp13 "/" Exp14;
Epreop.      Exp14 ::= UnaryOp Exp14;

Efunk.       Exp16 ::= Ident "(" ")";
Efunkpar.    Exp16 ::= Ident "(" [Exp] ")";
Earray.      Exp17 ::= "{" [Exp] "}";
EPostInc.    Exp18 ::= Exp20 "++" ;
EPostDec.    Exp18 ::= Exp20 "--" ;
ELitBool.    Exp19 ::= EBool;
ELitInt.     Exp19 ::= Integer;
EString.     Exp19 ::= String;
EVar.        Exp20 ::= Ident;
EArr.        Exp20 ::= Ident "[" Exp "]";


ETrue.  EBool ::= "true" ;
EFalse. EBool ::= "false";

coercions Exp 20 ;
separator Exp "," ;

-- Operators
--Address.     UnaryOp ::= "&" ;
--Indirection. UnaryOp ::= "*" ;
UnaryPlus.   UnaryOp ::= "+" ;
Negative.    UnaryOp ::= "-" ;
Logicalneg.  UnaryOp ::= "!" ;

Asgn.        AsgnOp ::= "=" ;
AsgnAdd.     AsgnOp ::= "+=" ;
AsgnSub.     AsgnOp ::= "-=" ;
AsgnMul.     AsgnOp ::= "*=" ;
AsgnDiv.     AsgnOp ::= "/=" ;

-- Comments
comment "/*" "*/" ;
comment "//";
comment "#";
